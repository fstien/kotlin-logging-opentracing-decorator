apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from sourceSets.main.java.srcDirs
    from sourceSets.main.kotlin.srcDirs
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    artifact("$buildDir/libs/lib-${version}.jar")
                }

                artifact androidSourcesJar
                artifact javadocJar

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Extension of kotlin-logging to write OpenTracing logs.'
                    url = 'https://github.com/fstien/kotlin-logging-opentracing-decorator'
                    licenses {
                        license {
                            name = 'Apache License 2.0'
                            url = 'https://github.com/fstien/kotlin-logging-opentracing-decorator/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'fstien'
                            name = 'Francois Stiennon'
                            email = 'francois.stiennon@gmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/fstien/kotlin-logging-opentracing-decorator.git'
                        developerConnection = 'scm:git:ssh://github.com/fstien/kotlin-logging-opentracing-decorator.git'
                        url = 'https://github.com/fstien/kotlin-logging-opentracing-decorator'
                    }
                }
            }
        }
    }
}

ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
    sign publishing.publications
}
